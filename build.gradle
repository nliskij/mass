plugins {
    id 'net.ltgt.errorprone' version '0.0.8'
    id 'edu.wpi.first.wpilib.versioning.WPILibVersioningPlugin' version '1.6'
}

// Ensure that the WPILibVersioningPlugin is setup by setting the release type, if releaseType wasn't
// already specified on the command line
if (!hasProperty('releaseType')) {
    WPILibVersion {
        releaseType = 'dev'
    }
}

def enableSimulation = hasProperty('makeSim')

ext.simulationInstallDir = "$rootDir/build/install/simulation"

allprojects {
    ext.enableSimulation = enableSimulation

    repositories {
        mavenCentral()
    }

    ext.ntcoreDep = { lang, classifier, extension = null ->
        return "edu.wpi.first.wpilib.networktables.$lang:NetworkTables:+:$classifier${extension == null ? '' : '@' + extension}"
    }

    ext.cscoreDep = { lang, classifier, extension = null ->
        return "edu.wpi.cscore.$lang:cscore:+:$classifier${extension == null ? '' : '@' + extension}"
    }

    ext.wpiUtilDep = { classifier ->
        return "edu.wpi.first.wpilib:wpiutil:+:$classifier@zip"
    }
}

subprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'checkstyle'

    checkstyle {
        toolVersion = "6.18"
        configFile = new File(rootDir, "styleguide/checkstyle.xml")
    }

    ext.armBuild = true

    // Disables doclint in java 8.
    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
    ext.setupWpilibRepo = { publishing ->
        publishing.repositories.maven {
            url = WPILibVersion.mavenLocalUrl
        }
    }
}

apply from: 'cppSettings.gradle'

apply from: 'ni-libraries/ni-libraries.gradle'

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}
