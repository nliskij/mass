plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'maven-publish'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact(shadowJar) {
                // The shadow plugin has the 'all' classifier. We don't want this, so use null instead
                classifier null
            }
            artifact(simDsSources) {
                classifier 'sources'
            }
            artifact(simDsJavadoc) {
                classifier 'javadoc'
            }
            groupId 'edu.wpi.first.wpilibj.simulation'
            artifactId 'SimDS'
            version WPILibVersion.version
        }
    }
    setupWpilibRepo(it)
}

mainClassName = 'edu.wpi.first.wpilibj.simulation.ds.Main'

dependencies {
    compile 'net.java.jinput:jinput:2.0.5'
    compile project(':simulation:JavaGazebo')
}

task simDsSources(type: Jar, dependsOn: classes) {
    description = 'Creates the sources jar for the SimDS'
    group = 'WPILib'
    classifier = 'sources'
    duplicatesStrategy = 'exclude'

    from sourceSets.main.allJava
}

task simDsJavadoc(type: Jar, dependsOn: javadoc) {
    description = 'Creates the javadoc jar for the SimDS'
    group = 'WPILib'
    classifier = 'javadoc'
    duplicatesStrategy = 'exclude'

    from javadoc.destinationDir
}

//we need to move the simulation jars to the install directory
task copyJars(type: Copy) {
    description = 'copy SimDS-all.jar to make simulation zip'
    group = 'WPILib Simulation'
    from shadowJar.archivePath
    into "$simulationInstallDir/jar"
}

build.dependsOn shadowJar
